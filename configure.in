AC_INIT(src/display.c)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(metacity, 2.3.13)

GETTEXT_PACKAGE=metacity
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

AM_MAINTAINER_MODE

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac

  if test "x$enable_ansi" = "xyes"; then
    case " $CFLAGS " in
    *[\ \	]-ansi[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -ansi" ;;
    esac

    case " $CFLAGS " in
    *[\ \	]-pedantic[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -pedantic" ;;
    esac
  fi
fi
changequote([,])dnl

ALL_LINGUAS=""
dnl AM_GNU_GETTEXT

## here we get the flags we'll actually use
PKG_CHECK_MODULES(METACITY, gtk+-2.0 >= 1.3.7)

CFLAGS="$METACITY_CFLAGS $CFLAGS"

found_sm=false
case "$METACITY_LIBS" in
 *-lSM*)
    found_sm=true
    ;;
 *)
  AC_CHECK_LIB(SM, SmcSaveYourselfDone, 
               AC_CHECK_HEADERS(X11/SM/SMlib.h,
                                METACITY_LIBS="-lSM -lICE $METACITY_LIBS" found_sm=true),
               , $METACITY_LIBS)
    ;;
esac

if test "$found_sm" = "true"; then
   AC_DEFINE(HAVE_SM)
fi

AM_CONDITIONAL(HAVE_SM, test "$found_sm" = "true") 

HOST_ALIAS=$host_alias
AC_SUBST(HOST_ALIAS)

AC_PATH_PROG(GDK_PIXBUF_CSOURCE, gdk-pixbuf-csource, no)

if test x"$GDK_PIXBUF_CSOURCE" = xno; then
  AC_MSG_ERROR([gdk-pixbuf-csource executable not found in your path - should be installed with GTK])
fi

AC_SUBST(GDK_PIXBUF_CSOURCE)

AC_OUTPUT([
Makefile
src/Makefile
src/wm-tester/Makefile
])
